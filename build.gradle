plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.1'
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id 'org.beryx.jlink' version '2.23.7'
}

group = 'io.github.ititus.pdx'
version = '1.0.0'

wrapper {
    gradleVersion '6.8.3'
}

java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_15
}

modularity {
    disableEffectiveArgumentsAdjustment()
}

application {
    mainModule = 'io.github.ititus.pdx'
    mainClass = 'io.github.ititus.pdx.Main'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.github.ititus:ititus-commons:1.4.6'
    implementation 'org.eclipse.collections:eclipse-collections-api:10.4.0'
    implementation 'org.eclipse.collections:eclipse-collections:10.4.0'
    implementation 'org.eclipse.collections:eclipse-collections-forkjoin:10.4.0'
    implementation 'io.github.java-diff-utils:java-diff-utils:4.9'

    testImplementation platform('org.junit:junit-bom:5.7.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.assertj:assertj-core:3.19.0'
    testImplementation 'org.mockito:mockito-core:3.9.0'
    // testImplementation 'org.eclipse.collections:eclipse-collections-testutils:10.4.0'
}

compileTestJava {
    moduleOptions {
        addModules = ['org.junit.jupiter.params']
        addReads = ['io.github.ititus.pdx': 'org.junit.jupiter.params']
    }
}

test {
    useJUnitPlatform()

    moduleOptions {
        addReads = ['io.github.ititus.pdx': 'org.junit.jupiter.params']
        addExports = [
                'org.junit.platform.commons/org.junit.platform.commons.util'   : 'ALL-UNNAMED',
                'org.junit.platform.commons/org.junit.platform.commons.logging': 'ALL-UNNAMED'
        ]
        addOpens = ['io.github.ititus.pdx/io.github.ititus.pdx.util.collection': 'org.junit.platform.commons']
    }
}

javafx {
    version = '16'
    modules = ['javafx.base', 'javafx.controls', 'javafx.graphics']
}

jlink {
    options = ['--strip-debug', '--no-header-files', '--no-man-pages', '--compress', '2']
    launcher {
        name = 'pdxtools'
    }
}
